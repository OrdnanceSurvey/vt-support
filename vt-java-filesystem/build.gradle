apply plugin: 'java'

version =  VERSION_NAME
group =  GROUP

compileJava {
    sourceCompatibility = rootProject.ext.sourceCompatibilityVersion
    targetCompatibility = rootProject.ext.targetCompatibilityVersion
}

configurations {
    provided
    testiCompile.extendsFrom testCompile
    testiRuntime.extendsFrom testRuntime
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    testi {
        compileClasspath += sourceSets.main.output + configurations.testCompile
        runtimeClasspath += output + compileClasspath + configurations.testRuntime
    }
}

// tag::dependencies[]
dependencies {
    compile project(':vt-java')

    compile 'com.google.guava:guava:23.3-jre'
    compile 'commons-io:commons-io:2.6'
    compile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'org.skyscreamer:jsonassert:1.5.0'
    testCompile deps.junit
}
// end::dependencies[]

//test {
//    // show standard out and standard error of the test JVM(s) on the console
//    testLogging.showStandardStreams = false
//}

task testi(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.testi.output.classesDirs
    classpath = sourceSets.testi.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}
check.dependsOn testi

apply from: rootProject.file('gradle/gradle-mvn-push-java.gradle')
